// Lexical rules
COMMENT = ${ "--" ~ (!"\n" ~ ANY)* }
WHITESPACE = _{ " " | "\t" | NEWLINE }

// Primitive helpers
character =
 _{ ASCII_ALPHA
  | "_"
  | "\'"
  | "\\"
  | '0'..'9'
  }
identifier_simple =
 @{ !"let"
  ~ !"rec"
  ~ !"fun"
  ~ !"sum"
  ~ !"\\lambda"
  ~ !"\\Sigma"
  ~ !"\\Pi"
  ~ !"0"
  ~ !"1"
  ~ !"_"
  ~ character+
  }
identifier_raw =
 @{ ("fun" ~ character+)
  | ("sum" ~ character+)
  | ("let" ~ character+)
  | ("rec" ~ character+)
  | ("0" ~ character+)
  | ("1" ~ character+)
  | ("_" ~ character+)
  }
identifier = { identifier_simple | identifier_raw }
constructor_name = @{ ASCII_ALPHA_UPPER ~ identifier? }
pi = _{ "Π" | "\\Pi" }
sigma = _{ "Σ" | "\\Sigma" }
lambda = _{ "λ" | "\\lambda" }
let_or_rec = { "let" | "rec" }
one = { "1" }
unit = { "0" }
universe = { "U" }
void = { EOI }

// Extracted helpers
typed_abstraction = _{ identifier ~ ":" ~ expression ~ "." ~ expression }
branches = _{ "(" ~ (constructor ~ ("|" ~ constructor)*)? ~ ")" }

// Atomic expressions
pi_type = { pi ~ typed_abstraction }
lambda_expression = { lambda ~ identifier ~ "." ~ expression }
constructor = { constructor_name ~ expression }
sigma_type = { sigma ~ typed_abstraction }
function = { "fun" ~ branches }
sum = { "sum" ~ branches }
variable = { identifier }
atom =
  { universe
  | constructor
  | variable
  | function
  | sum
  | one
  | unit
  | pi_type
  | sigma_type
  | lambda_expression
  | "(" ~ expression ~ ")"
  }

// Higher-level expressions
application = { atom ~ expression }
pair = { atom ~ "," ~ expression }
first = { atom ~ ".1" }
second = { atom ~ ".2" }
expression =
 { declaration
 | application
 | first
 | second
 | pair
 | atom
 | void
 }

// Declaration
declaration =
 { let_or_rec
 ~ identifier
 ~ ":" ~ expression
 ~ "=" ~ expression
 ~ ";" ~ expression
 }
